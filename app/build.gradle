apply plugin: 'com.android.application'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId "com.wassim.mvvmboilerplate"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            testCoverageEnabled true
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    // needed because of https://github.com/robolectric/robolectric/issues/2230
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    // needed because of this https://github.com/square/okio/issues/58
    lintOptions {
        disable 'InvalidPackage'
    }

}

jacocoAndroidUnitTestReport {
    excludes += ['**/util/*.*',
                 '**/ui/**/*.*',
                 '**/data/remote/*.*',
                 '**/databinding/*.*',
                 '**/injection/**/*.*',
                 '**/*Factory*',
                 '**/*Insert_movie*',
                 '**/*AutoValue_*.*',
                 '**/$AutoValue_*.*',
                 '**/*Builder*.*']
}

dependencies {

    /////// versions names

    final SUPPORT_LIBRARY_VERSION = '24.2.1'
    final RETROFIT_VERSION = '2.1.0'
    final OKHTTP_VERSION = '3.6.0'
    final GLIDE_VERSION = '3.7.0'
    final STETHO_VERSION = '1.4.2'
    final LEAK_CANARY_VERSION = '1.6-SNAPSHOT'
    final DAGGER_VERSION = '2.5'
    final MOCKITO_VERSION = '2.6.2'
    final HAMCREST_VERSION = '1.3'
    final ESPRESSO_VERSION = '2.2.1'
    final RUNNER_VERSION = '0.4'
    final BUTTERKNIFE_VERSION = '8.4.0'
    final AUTO_VALUE_VERSION = '1.3'
    final AUTO_VALUE_GSON_VERSION = '0.4.6'
    final PLAY_SERVICES_VERSION = '8.3.0'
    final JUNIT_VERSION = '4.12'

    /////// defs

    def daggerCompiler = "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    def jUnit = "junit:junit:$JUNIT_VERSION"
    def mockito = "org.mockito:mockito-core:$MOCKITO_VERSION"

    /////// app dependencies

    // some support libs
    //noinspection GradleDependency
    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    //noinspection GradleDependency
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    //noinspection GradleDependency
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    //noinspection GradleDependency
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"

    // some play services
    //noinspection GradleDependency
    compile "com.google.android.gms:play-services-gcm:$PLAY_SERVICES_VERSION"

    // some database
    compile("com.squareup.sqlbrite:sqlbrite:1.1.1") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // some networking
    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava:$RETROFIT_VERSION"
    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    compile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    compile "com.github.bumptech.glide:glide:$GLIDE_VERSION"

    // some threading
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    // some injections
    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    annotationProcessor daggerCompiler
    testAnnotationProcessor daggerCompiler
    androidTestAnnotationProcessor daggerCompiler

    // some logging and performance
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile "com.facebook.stetho:stetho:$STETHO_VERSION"
    compile "com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION"
    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"

    // some annotation processing
    provided "com.jakewharton.auto.value:auto-value-annotations:$AUTO_VALUE_VERSION"
    provided "com.ryanharter.auto.value:auto-value-gson:$AUTO_VALUE_GSON_VERSION"
    annotationProcessor "com.google.auto.value:auto-value:$AUTO_VALUE_VERSION"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.4-rc2'
    annotationProcessor "com.ryanharter.auto.value:auto-value-gson:$AUTO_VALUE_GSON_VERSION"
    annotationProcessor 'com.squareup:javapoet:1.7.0'

    // some Instrumentation Test dependencies
    androidTestCompile jUnit
    androidTestCompile mockito
    androidTestCompile "org.mockito:mockito-android:$MOCKITO_VERSION"
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestCompile "com.android.support.test:runner:$RUNNER_VERSION"
    androidTestCompile "com.android.support.test:rules:$RUNNER_VERSION"

    // some Unit Test dependencies
    testCompile jUnit
    testCompile mockito
    testCompile "org.hamcrest:hamcrest-core:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-library:$HAMCREST_VERSION"
    testCompile "org.hamcrest:hamcrest-integration:$HAMCREST_VERSION"
    testCompile 'org.robolectric:robolectric:3.1'
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}
